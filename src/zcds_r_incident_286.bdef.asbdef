managed implementation in class zbp_cds_r_incident_286 unique;
strict ( 2 );
with draft;
define behavior for ZCDS_R_INCIDENT_286 alias Incident
persistent table zdt_inct_286
draft table zdt_inct_286_d
lock master
total etag LastChangedAt
etag master LocalLastChangedAt
authorization master ( global, instance )
{
  create ( authorization : global );
  update;
  delete;
  field ( numbering : managed, readonly ) IncUuid;

  field ( readonly )
  IncidentId,
  CreationDate,
  LastChangedAt,
  LocalCreatedAt,
  LocalCreatedBy,
  LocalLastChangedAt,
  LocalLastChangedBy;

  field ( mandatory : create ) Title, Description, Priority, Status;
  //
  //  field ( mandatory : update ) Title, Status, Priority;
  association _History { create ( features : instance ); with draft; }

  determination setValuesInitial on modify { create; }

  determination setHistoryInitial on save { create; }

  determination setChangeDate on save { update; create; }

  determination createInitialHistory on save { create; }


  action ( features : instance, authorization : update ) NewStatus parameter ZCDS_NEW_STATUS_286 result [1] $self;

  internal action setHistoryInc;

  side effects
  { action NewStatus affects $self;
  }

  validation validaCamposObligatorios on save { update; create; }

  determine action validaCamposMandatorios { validation validaCamposObligatorios; }

  draft action Resume;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;

  draft determine action Prepare
  {
    validation validaCamposObligatorios;

  }

  mapping for zdt_inct_286
    {
      Incuuid            = inc_uuid;
      IncidentId         = incident_id;
      Title              = title;
      Description        = description;
      Status             = status;
      Priority           = priority;
      CreationDate       = creation_date;
      ChangedDate        = changed_date;
      Responsable        = responsable;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}

define behavior for ZCDS_INC_HISTORY_286 alias History
persistent table zdt_inct_h_286
draft table zdt_inct_h_286_d
lock dependent by _Incident
authorization dependent by _Incident
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) HisUuid;
  field ( readonly )
  IncUuid,
  LocalCreatedBy,
  LocalCreatedAt,
  LocalLastChangedBy,
  LocalLastChangedAt,
  LastChangedAt;



  association _Incident { with draft; }
  mapping for zdt_inct_h_286
    {
      HisUuid            = his_uuid;
      IncUuid            = inc_uuid;
      HisId              = his_id;
      PreviousStatus     = previous_status;
      NewStatus          = new_status;
      text               = Text;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}